typeset -A __PJL
__PJL[ITALIC_ON]=$'\e[3m'
__PJL[ITALIC_OFF]=$'\e[23m'

setopt PROMPT_SUBST

autoload -Uz vcs_info
#:vcs_info:vcs-string+hook-name:user-context:repo-root-name
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' use-simple true
zstyle ':vcs_info:*' formats '%F{244}[%b%m%c%u%F{244}]'
zstyle ':vcs_info:*' stagedstr "%F{green}" #• default 'S'
zstyle ':vcs_info:*' unstagedstr "%F{yellow}" #•󱈸 default 'U'
zstyle ':vcs_info:git*:*' actionformats '%F{244}[%b|%F{cyan}%a%F{244}]'
zstyle ':vcs_info:git+set-message:*' hooks git-untracked

function +vi-git-untracked() {
emulate -L zsh
if [[ -n $(git rev-parse --is-inside-work-tree 2>/dev/null) && -n $(git ls-files --exclude-standard --others 2> /dev/null) ]]; then
  hook_com[unstaged]+="%F{red}%F" #
fi
}

# Remember each command we run.
function -record-command() {
  __PJL[LAST_COMMAND]="$2"
}
add-zsh-hook preexec -record-command

# Update vcs_info (slow) only after a command that probably changed it
function -maybe-show-vcs-info() {

  # LAST = first Word of the last executed command line
  local LAST=${${__PJL[LAST_COMMAND]}[(w)1]}

  # In case user just hit enter, overwrite LAST_COMMAND, because preexec
  # wont run and it will otherwise linger.
  __PJL[LAST_COMMAND]="<clear>"

  case "$LAST" in
    cp|git|mv|rm|touch)
      vcs_info
      ;;
    *)
      ;;
  esac
}
add-zsh-hook precmd -maybe-show-vcs-info

# show vcs info when entering git directory
function -show-vcs-info-in-git-dir() {
  if [[ -n $(git rev-parse --is-inside-work-tree 2>/dev/null) ]]; then
    vcs_info
  else
    vcs_info_msg_0_=""
  fi
}
add-zsh-hook chpwd -show-vcs-info-in-git-dir

# add python virtuan env
function -venv_info() {
    if [[ -n $VIRTUAL_ENV ]]; then
        export VENV_PROMPT="(${VIRTUAL_ENV:t}) "
    else
        export VENV_PROMPT=""
    fi
}
add-zsh-hook precmd -venv_info

# replace $HOME with ~
function rprompt_path() {
  local last_dir="/${PWD##*/}"
  local rpath="${PWD%$last_dir}"
  local rdir="${rpath/$HOME/~}"
  echo ${rdir}
}

# show shell level in the prompt: >, >>, >>>
function shell_level() {
  local level_char="❯"
  local level_start=1
  local level_string=""

  test -n "$TMUX" && level_start=2

  for i in {$level_start..$SHLVL}; do
    level_string+="$level_char"
  done

  echo ${level_string}
}

# Elapsed time
#
typeset -F SECONDS
function -record-start-time() {
  emulate -L zsh
  ZSH_START_TIME=${ZSH_START_TIME:-$SECONDS}
}
add-zsh-hook preexec -record-start-time

function -report-start-time() {
emulate -L zsh
if [ $ZSH_START_TIME ]; then
  local DELTA=$(($SECONDS - $ZSH_START_TIME))
  local DAYS=$(( ~~($DELTA / 86400) ))
  local HOURS=$(( ~~(($DELTA - $DAYS * 86400) / 3600) ))
  local MINUTES=$((~~(($DELTA - $DAYS * 86400 - $HOURS * 3600) / 60) ))
  local SECS=$(($DELTA - $DAYS * 86400 - $HOURS * 3600 - $MINUTES * 60 ))
  local ELAPSED=''
  test "$DAYS" != '0' && ELAPSED="${DAYS}d"
  test "$HOURS" != '0' && ELAPSED="${ELAPSED}${HOURS}h"
  test "$MINUTES" != '0' && ELAPSED="${ELAPSED}${MINUTES}m"
  if [ "$ELAPSED" = '' ]; then
    SECS="$(print -f "%.2f" $SECS)s"
  elif [ "$DAYS" != '0' ]; then
    SECS=''
  else
    SECS="$((~~$SECS))s"
  fi
  ELAPSED="${ELAPSED}${SECS}"
  export RPROMPT="%F{blue}%{$__PJL[ITALIC_ON]%}${ELAPSED}%{$__PJL[ITALIC_OFF]%}%f $RPROMPT_BASE"
  unset ZSH_START_TIME
else
  export RPROMPT="$RPROMPT_BASE"
fi
}
add-zsh-hook precmd -report-start-time


# Construct PROMPTS

local RPATH='$(rprompt_path)'
export PS_SHLVL='$(shell_level)'
export RPROMPT_BASE="\${vcs_info_msg_0_}%F{blue}${RPATH}%f"
export RPROMPT=$RPROMPT_BASE
export PS1="%F{green}\${VENV_PROMPT}%F{blue}%1~%F{red} ${PS_SHLVL}%f "

# vim: ft=zsh sw=2 foldmethod=marker foldmarker={{{,}}}
